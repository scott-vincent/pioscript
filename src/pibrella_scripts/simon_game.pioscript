#
# This is the Main activity
#
use_addon Pibrella
clear_screen
start_activity Endless Fun


#
# This is the 'Endless Fun' activity
#
activity Endless Fun
do_activity New Game
repeat


#
# This is the 'New Game' activity
#
activity New Game
print
print "Simon says:"
print "   Press any coloured button to start a new game"
print "   or the red button on the Pibrella to quit"

start_activity Attract Mode
while not pressed(InputA) and not pressed(InputB) and not pressed(InputC) and not pressed(InputD)
	# If the button on the Pibrella is pressed we quit	
	if pressed(Button)
		stop_activity Attract Mode
		turn_off *
		exit
	end_if
	wait .1
end_while
stop_activity Attract Mode
turn_off *

# Initialise a new game
set len = 0
set correct = true
print
print "Here we go"

# Play the game
while correct
	# Add another note to the sequence
	set seq[len] = int(rand() * 4)
	set len = len + 1

	# Speed up if the player is doing well
	if len > 14
		set noteLen = 0.3
	else if len > 9
		set noteLen = 0.4
	else if len > 4
		set noteLen = 0.5
	else
		set noteLen = 0.7
	end_if

	# Show the sequence to the player
	print
	print "Memorise this sequence"
	do_activity Show Sequence

	# Wait for the player to input the sequence
	print "Ok, your turn"
	do_activity Input Sequence

	if correct
		print "Well done"
		play_song 20 c0 d0 e0 f0 g0 a0 b0 c1
	end_if
end_while

# Game over
print "Oh no, you made a mistake!"
sound_buzzer 1.5
set score = len - 1
print "Your score was " score
print "You should have played this"
do_activity Show Sequence
print "Game Over"
wait 1


#
# This is the 'Attract Mode' activity
#
activity Attract Mode
turn_off OutputF
turn_on OutputE
wait .1
turn_off OutputE
turn_on OutputG
wait .1
turn_off OutputG
turn_on OutputH
wait .1
turn_off OutputH
turn_on OutputF
wait .1
repeat


#
# This is the 'Show Sequence' activity
#
activity Show Sequence
wait 1
for (i = 0) (i < len) (i = i + 1)
	if (seq[i] = 0)
		turn_on OutputE
		play_note c0 noteLen
		turn_off OutputE
	else if (seq[i] = 1)
		turn_on OutputF
		play_note e0 noteLen
		turn_off OutputF
	else if (seq[i] = 2)
		turn_on OutputG
		play_note g0 noteLen
		turn_off OutputG
	else if (seq[i] = 3)
		turn_on OutputH
		play_note c1 noteLen
		turn_off OutputH
	end_if
	wait .05
end_for


#
# This is the 'Input Sequence' activity
#
activity Input Sequence
for (i = 0) ((i < len) and correct) (i = i + 1)
	do_activity Player Button Press
	set correct = (seq[i] = playerPress)
end_for


#
# This is the 'Player Button Press' activity
#
activity Player Button Press
set playerPress = -1
while playerPress = -1
	if pressed(InputA)
		turn_on OutputE
		play_note c0 noteLen
		turn_off OutputE
		set playerPress = 0
	else if pressed(InputB)
		turn_on OutputF
		play_note e0 noteLen
		turn_off OutputF
		set playerPress = 1
	else if pressed(InputC)
		turn_on OutputG
		play_note g0 noteLen
		turn_off OutputG
		set playerPress = 2
	else if pressed(InputD)
		turn_on OutputH
		play_note c1 noteLen
		turn_off OutputH
		set playerPress = 3
	end_if
	wait .05
end_while
